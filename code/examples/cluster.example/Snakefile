# 무시

rule all:
    input: "aggregate.txt"

# 잠재적으로 알 수 없는 수의 파일 만들기
checkpoint make_many_files:
    output: directory("many_files")
    shell: """
        mkdir -p many_files
        echo 1 > many_files/1.out
        echo 2 > many_files/2.out
    """

#
# 'make_many_files' 체크포인트 규칙이 실행된 *후에만* 파일 이름을 로드하는 Python 함수 만들기
#

def load_many_files(wc):
    # 'make_many_files'의 결과를 기다립니다.
    checkpoint_output = checkpoints.make_many_files.get(**wc).output[0]
    
    # 이것은 'make_many_files'가 완료된 *후에만* 실행됩니다.
    many_files_names = glob_wildcards('many_files/{name}.out').name

    return expand('many_files/{name}.out', name=many_files_names)

# 'load_many_files'를 입력으로 사용합니다. 이 규칙은 'make_many_files'가 실행된 *후에만* 실행됩니다.
rule work_with_many_files:
    input:
        load_many_files
    output: "aggregate.txt"
    shell:
        "cat {input} > {output}"
